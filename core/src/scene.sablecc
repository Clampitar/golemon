Package gdx.game.Scene;

Helpers

  letter = ['a'..'z'];
  string_char = [[32..127] - ['"'+'\']];
  escape_char = '\' [32..127];
  digit = ['0'..'9'];
  eol = 13 | 10 | 13 10;
  tab = 9;
  not_eol = [[0..0xffff]-[13 + 10]];

Tokens

  /* keywords */

  print = 'print';
  println = 'println';
  true = 'true';
  false = 'false';
  if = 'if';
  then = 'then';
  else = 'else';
  end = 'end';
  return = 'return';
  struct = 'struct';
  do = 'do';
  var = 'var';
  while = 'while';
  for = 'for';
  import = 'import';
  
  int_type = 'int';
  bool_type = 'bool';
  string_type = 'string';
  void_type = 'void';
  
  /*methodes natives*/
  
  //say = 'say';
  frame_advance = 'frameAdvance';
  walk = 'walk';
  move_cam = 'moveCam';

  /*punctuation */
  
  sc = ';';
  assign = '=';
  plus = '+';
  minus = '-';
  mult = '*';
  plus_inc = '++';
  minus_inc = '--';
  modulo = '%';
  lt = '<';
  gt = '>';
  eq = '==';
  comma = ',';
  dot = '.';
  quote = '"';

  l_par = '(';
  r_par = ')';


  ident = letter (letter | digit | '_')*;
  string =  '"' (string_char | escape_char)* '"' ;
  integer = '-'? digit+;
  float = '-'? digit* '.' digit+;

  blank = (' ' | eol | tab)+;
  comment = '#' not_eol*;

Ignored Tokens
  blank, comment;


Productions

  prog =
    [struct_decls]:struct_decl* [fun_decls]:fun_decl* [insts]:inst*;

  struct_decl =
    struct ident fields end;

  fields =
    field [additional_fields]:additional_field*;

  additional_field =
    comma field;

  field =
    ident;
    
  type =
    {int} int_type |
    {bool} bool_type |
    {string} string_type |
    {void} void_type;

  fun_decl =
    type [name]:ident l_par params? r_par do fun_body end;

  fun_body =
    [insts]:inst*;

  inst =
    {assigner} assigner sc |
    {if} if exp then_part else_part? end |
    {block} do [insts]:inst* end |
    {while} while exp do while_body end |
    {for} for l_par [decl]:assigner? [first_sc]:sc [cond]:exp? [second_sc]:sc [iter]:assigner? r_par do while_body end |
    {return} return exp? sc |
    {fun_call} ident l_par args? r_par sc |
    {print_exp} print exp sc |
    {println} println sc |
   // {say} say exp sc |
   
    {frame_advance} frame_advance exp? sc |
    {walk} walk l_par args r_par sc |
    {move_cam} move_cam l_par args r_par sc;
    
  assigner =
    {decl} var ident assign exp |
    {assign} ident assign exp |
    {field_assign} [left]:unary dot ident assign exp | // n'est pas utilisé
    {increment} increment;
    
  while_body =
    [insts]:inst*;

  then_part =
    then [insts]:inst*;

  else_part =
    else [insts]:inst*;

  exp =
    {eq} [left]:exp eq [right]:additive_exp |
    {lt} [left]:exp lt [right]:additive_exp |
    {gt} [left]:exp gt [right]:additive_exp |
    {le} [left]:exp lt assign [right]:additive_exp |
    {ge} [left]:exp gt assign [right]:additive_exp |
    {simple} additive_exp;

  additive_exp =
    {add} [left]:additive_exp plus [right]:mult_exp |
    {sub} [left]:additive_exp minus [right]:mult_exp |
    {simple} mult_exp;
    
  mult_exp =
  	{mult} [left]:mult_exp mult [right]:unary |
  	{modulo} [left]:mult_exp modulo [right]:unary |
  	{simple} unary;

  unary =
    {field_access} [left]:unary dot ident |
    {simple} term;

  term =
    {true} true |
    {false} false |
    {integer} minus? integer |
    {string} string |
	{float} float |	
    {var} ident |
    {par} l_par exp r_par |
    {fun_call} ident l_par args? r_par |
    {struct} struct ident l_par args? r_par |
    {increment} increment;
 //   {fun} fun l_par params? r_par do fun_body end;

  increment =
    {pre_add} plus_inc ident |
    {post_add} ident plus_inc |
    {pre_sub} minus_inc ident |
    {post_sub}  ident minus_inc;

  params =
    param [additional_params]:additional_param*;

  additional_param =
    comma param;

  param =
    type ident;

  args =
    arg [additional_args]:additional_arg*;

  additional_arg =
    comma arg;

  arg =
    exp;


